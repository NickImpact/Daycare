buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}

plugins {
    id 'com.qixalite.spongestart' version '1.6.2'
    id 'maven'
}

apply plugin: 'net.minecraftforge.gradle.forge'
version '0.8.1-BETA'

group 'com.nickimpact.daycare'

ext {
    pluginversion = '0.11.0-BETA'
    spongeapiversion = '7.1.0-SNAPSHOT'
    completeversion = "S${spongeapiversion.substring(0, 5)}-${pluginversion}"
}

sourceCompatibility = targetCompatibility = "1.8" // Need this here so eclipse task generates correctly.
compileJava {
    sourceCompatibility = targetCompatibility = "1.8"
}

minecraft {
    version = "1.12.2-14.23.3.2655"
    runDir = "run"
    mappings = "snapshot_20171007"
    useDepAts = true
    makeObfSourceJar = false
}

spongestart{
    eula true

    //optional settings, takes latest version by default
    minecraft '1.12.2'
    type 'bleeding'
    spongeForgeVersion '1.12.2-2555-7.0.0-BETA-2774'
    spongeVanillaVersion '1.12.2-7.0.0-BETA-374'
}

repositories {
    mavenCentral()
    maven {
        name = 'sponge'
        url = 'http://repo.spongepowered.org/maven'
    }
    ivy {
        url "https://download.nodecdn.net/containers"
        layout "pattern", {
            artifact "/[organisation]/[module]/release/[revision]/[ext]"
        }
    }
}

dependencies {
    compileOnly fileTree(dir: 'libs', include: '*.jar')
    compileOnly 'org.spongepowered:spongeapi:7.1.0-SNAPSHOT'
    compile 'reforged:server:6.3.4@Pixelmon-1.12.2-6.3.4-server.jar'
    compileOnly "org.projectlombok:lombok:1.16.10"
    compileOnly group: 'com.zaxxer', name: 'HikariCP', version: '2.5.1'
    testCompile "junit:junit:4.11"
}

jar {
    archiveName = "Daycare-${completeversion}.jar"
}

//Fix forges AT breaking everything because the AT checker is stupid
[tasks.deobfMcMCP, tasks.deobfMcSRG].each { it.failOnAtError = false }

processResources
{
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}
